import crack.ascii escape, hex;
import crack.db.mongo BSON, MONGO_CONN_LOCAL, MongoSync;
import crack.enc.json JSONParser, JSONStringFormatter, ParseException,
                      JSONObject, JSONString, JSONInt, JSONArray;
import crack.hash.murmur3 Murmur3_32;
import crack.io cerr, cout, FStr;
import crack.lang CString, Buffer;
import crack.logger Logger;
import crack.runtime errno, exit;
import crack.sys strerror;
import dawn.jsonrpc JSONRpcHandler, JSONEcho, JSONMethod, JSONError;
import dawn.scgi SCGIRequestHandler, Error, SCGIRequest;
import test.shorten ShortUrl;
import whip.bson_serializer BSONBufferSerializer, BSONBufferMaterializer;
import whip.json_serializer JSONBufferSerializer, JSONBufferMaterializer;

CString ns = "dawn.shorten";

class JSONShortener : JSONMethod {
  BSON _query;
  MongoSync _conn;
  Logger _logger;

  oper init(MongoSync c, Logger l) : _conn = c, _logger = l {
    name = "shorten";
    type = "text/json";
    code = 200;
    done = false;
  }

  // Receive request object that is a valid jsonrpc 2 request with our method name
  JSONObject exec(SCGIRequest req, JSONObject rpc) {
    done = false;
    params := rpc["params"];
    rpc.deleteKey("method"); // re-using Request object for the reply
    rpc.deleteKey("params");
    
    JSONString url = null;
    ShortUrl record = null;

    if (params.isa(JSONString))
      url = JSONString.cast(params);
    else if (params.isa(JSONArray)) {
      A := JSONArray.cast(params);
      if (A.count() > 0 && A[0].isa(JSONString))
        url = JSONString.cast(A[0]);
    }

    if (url is null) {
      JSONError(req, 400, "-1") `$name method takes one string argument`;
      done = true;
    } else {
      _query = BSON();
      _query.appendString("url", String.cast(url));
      _query.finish();
      _logger.debug(FStr() `Submitting query $_query`);

      cursor := _conn.query(ns, 0, 0, 1, _query, null);

      if (cursor.next()) {
        mt := BSONBufferMaterializer(cursor.data());
        mt.setStrict(false);
        record = ShortUrl(mt);
        _logger.debug(FStr() `Got record $(mt.getBSON())`);
      }

      if (record is null) { // We have to create it
        record = ShortUrl();
        record.url = url;
        record.short = hex(Murmur3_32(url).digest());
        sr := BSONBufferSerializer();
        record.serialize(sr, null);
        sr.finish();
        _logger.debug(FStr() `Inserted record $(sr.getBSON())`);
        _conn.insert(ns, sr.getBSON());
      }
    }

    if (!done && !(record is null)) {
      res := JSONObject();
      res["url"] = record.url;
      res["short"] = record.short;
      rpc["result"] = res;
    }
    return rpc;
  }
}


// An url shortener JSONRPC service 
class ShortenerHandler : JSONRpcHandler {
  BSON _query;
  MongoSync _conn;
  ShortUrl _record;
  Buffer _short;

  oper init(String host, int port, Logger l) : JSONRpcHandler(l) {
    // Connect to localhost over TCP/IP
    _conn = MongoSync("localhost", 27017, true);
    _conn.setAutoReconnect(true);
    addMethod(JSONEcho());
    addMethod(JSONShortener(_conn, l));
  }

  bool onGet(SCGIRequest req) {
    path := req.inHeaders['PATH_INFO'];
    _logger.debug(FStr() `path.size=$(path.size), path[0] = $(path[0])`);
    if (path.size > 1 && path[0] == b'/' && path.size < 10) {
      _short = Buffer(path.buffer + 1, path.size - 1);
      _query = BSON();
      _query.appendString("short", String(_short));
      _query.finish();

      _record = null;
      _logger.debug(FStr() `Submitting query $_query`);

      cursor := _conn.query(ns, 0, 0, 1, _query, null);

      if (cursor.next()) {
        mt := BSONBufferMaterializer(cursor.data());
        mt.setStrict(false);
        _record = ShortUrl(mt);
        _logger.debug(FStr() `Got record $(mt.getBSON())`);
        req.sendRedirect(200, _record.url);
      }
      else
        Error(req, 404) `Short key $_short not found`;
    }
    else
      Error(req, 400) `Malformed request $(req.inHeaders['SCRIPT_URL'])`;
    return true;
  }

}
