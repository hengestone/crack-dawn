import crack.cont.hashmap HashMap;
import crack.enc.json JSONParser, JSONStringFormatter, ParseException,
                      JSONObject, JSONString, JSONInt, JSONArray;
import crack.io cerr, FStr, StringFormatter, Formatter;
import crack.lang Buffer, Exception;
import crack.logger Logger;
import dawn.scgi SCGIRequestHandler, Error, SCGIRequest;

// JSON Error formatter.  Returns a simple JSONRPC message to the client
// error reply with the message as its parcel.
class JSONError : StringFormatter {
    SCGIRequest req;
    int code;
    Buffer id;

    oper init(SCGIRequest req, int code, Buffer id) : req = req, code = code {}

    void leave() {
        result := createString();
        req.sendReply(code, "text/json",
        FStr() `{"error": "$result", "id": $id, "jsonrpc": "2.0"}`);
    }
}

@abstract class JSONMethod {
  String name;
  int code;
  Buffer type;
  bool done;
  @abstract JSONObject exec(SCGIRequest req, JSONObject rpc);
}

class JSONEcho : JSONMethod {
  oper init() {
     name = "echo";
     type = "text/json";
     code = 200;
     done = false;
  }

  // Receive request object that is a valid jsonrpc 2 request with our method name
  JSONObject exec(SCGIRequest req, JSONObject rpc) {
    params := rpc["params"];
    rpc.deleteKey("method");
    rpc.deleteKey("params");
    rpc["result"] = params;
    return rpc;
  }
}

class JSONRpcHandler : SCGIRequestHandler {
    JSONParser _parser = {};
    JSONStringFormatter _jfmt = {};
    HashMap[Buffer, JSONMethod] _methods = {};
    Logger _logger;

    oper init (Logger l) : _logger=l {
    }

    void addMethod(JSONMethod m) {
      _methods[m.name] = m;
    }

    HashMap[Buffer, JSONMethod] getMethods() {
      return _methods;
    }
    
    bool onGet(SCGIRequest req) {
        return false;
    }

    bool onPut(SCGIRequest req) {
        return false;
    }

    // Validate request structure
    bool _validRequest(JSONObject rpc) {
      if (rpc.get("method", null) is null ||
          rpc.get("params", null) is null ||
          rpc.get("id", null) is null) return false;
      return true;
    }
    
    bool onPost(SCGIRequest req) {
      JSONMethod mt = null;
      JSONObject result;
      _logger.debug("JSONRpcHandler Processing POST request");
      String method = null;

      try {
        JSONObject result;
        int id;
        parseVal := _parser.parse(req.data);
        if (parseVal.result && parseVal.result.isa(JSONObject)) {
          _logger.debug("Parsed JSON OK");
          jo := JSONObject.cast(parseVal.result);
          jmethod := jo.get("method", null);
          if (jmethod.isa(JSONString)) {
            jmt := JSONString.cast(jmethod);
            mt = _methods.get(jmt, null);
            method = String(jmt);
          }
          params := jo.get("params", null);
          id := jo.get("id", null);
          if (mt is null || params is null || id is null) {
            JSONError(req, 400, "-1")
              `Invalid JSONRPC request, need 'method', 'params' and 'id'`;
          } else if (mt is null) {
            JSONError(req, 405, "-1") `Unknown method $method`;
          }
          else {
            _jfmt.format(jo);
            _logger.debug(FStr() `Calling method with $(_jfmt.string())`);
            result = mt.exec(req, jo);
            if (!mt.done) {
              _jfmt.format(result);
              req.sendReply(mt.code, mt.type, _jfmt.string());
            }
          }
        }
        else throw ParseException("Toplevel structure must be a JSON Object", 1, 1);
      } catch (ParseException px) {
        _logger.info("Error2");
        JSONError(req, 400, "-1") `Error parsing JSONRPC request: $px`;
      }
      catch (Exception ex) {
        _logger.error(FStr() `Error processing request: $(ex.text). Data was\n$(req.data)`);
        JSONError(req, 400, "-1") `Error processing request: $(ex.text)`;
      }
      _logger.debug("JSONRpcHandler DONE Processing POST request");
      return true;
    }

    bool oper call(SCGIRequest req) {
        if (req.method == 'GET')
            return onGet(req);
        else if (req.method == 'POST')
            return onPost(req);
        else if (req.method == 'PUT')
            return onPut(req);
        else return false;
    }

    void formatTo(Formatter fmt) {
      fmt.write("JSONRpcHandler(");
      bool first = true;
      for (item :in _methods) {
        if (first) first = false;
        else fmt.write(", ");
        fmt.write(item.key);
      }
      fmt.write(")");
    }
}

#~ class JSONGetMethods : JSONMethod {
#~   JSONRpcHandler _handler;
#~   oper init(JSONRpcHandler h) : _handler = h {
#~      name = "methods";
#~      type = "text/json";
#~      code = 200;
#~      done = false;
#~   }
#~ 
#~   // Receive request object that is a valid jsonrpc 2 request with our method name
#~   JSONObject exec(SCGIRequest req, JSONObject rpc) {
#~     params := rpc["params"];
#~     rpc.deleteKey("method");
#~     rpc.deleteKey("params");
#~     res := JSONArray();
#~     for (item :in _handler.getMethods())
#~       JSONArray.append(JSONString(item.key));
#~ 
#~     rpc["result"] = res;
#~     
#~     return rpc;
#~   }
#~ }
